load("@io_bazel_rules_go//go:def.bzl", "go_binary", "go_library")
load("@io_bazel_rules_docker//go:image.bzl", "go_image")
load("@io_bazel_rules_docker//container:container.bzl", "container_bundle", "container_image", "container_layer", "container_push")

# Go image. "bazel build //:cip-docker-image.tar" results in a tarfile that is
# "docker load -i"-able, but the image is automatially named
# "bazel:cip-docker-image". To get around this, we use the container_bundle rule
# to set the image name directly.
go_image(
    name = "cip-docker-image",
    embed = [":go_default_library"],
    importpath = "github.com/kubernetes-sigs/k8s-container-image-promoter",
    goarch = "amd64",
    goos = "linux",
    pure = "on",
    base = "@google-sdk-base//image",
)

container_image(
    name = "cip-with-gcloud",
    base = ":cip-docker-image",
    entrypoint = ["/bin/bash", "-c"],
    layers = [":cip-util"],
    env = {"PATH": "/cip:/cip/util:${PATH}"},
    symlinks = {"/cip/cip": "/app/cip-docker-image.binary"},
)

container_layer(
    name = "cip-util",
    directory = "/cip/util",
    files = ["util/multirun.sh"],
)

# Invoke with "bazel build //:cip-docker-loadable.tar". Then you can run "docker
# load -i bazel-bin/cip-docker-loadable.tar" to load it into the local Docker
# daemon.
container_bundle(
    name = "cip-docker-loadable",
    images = {
        "{STABLE_IMG_REGISTRY}/{STABLE_IMG_REPOSITORY}/{STABLE_IMG_NAME}:latest": "//:cip-with-gcloud",
        "{STABLE_IMG_REGISTRY}/{STABLE_IMG_REPOSITORY}/{STABLE_IMG_NAME}:{STABLE_IMG_TAG}": "//:cip-with-gcloud",
    },
)

load("@bazel_gazelle//:def.bzl", "gazelle")

# gazelle:prefix github.com/kubernetes-sigs/k8s-container-image-promoter
gazelle(name = "gazelle")

go_library(
    name = "go_default_library",
    srcs = ["cip.go"],
    importpath = "github.com/kubernetes-sigs/k8s-container-image-promoter",
    visibility = ["//visibility:private"],
    deps = [
        "//lib/dockerregistry:go_default_library",
        "//lib/stream:go_default_library",
    ],
)

go_binary(
    name = "cip",
    embed = [":go_default_library"],
    visibility = ["//visibility:public"],
    x_defs = {
        "main.GitCommit": "{STABLE_GIT_COMMIT}",
        "main.GitDescribe": "{STABLE_GIT_DESC}",
    },
)

container_push(
    name = "push-cip-latest",
    image = ":cip-with-gcloud",
    format = "Docker",
    registry = "{STABLE_IMG_REGISTRY}",
    # repository should end with the name of the image to use.
    repository = "{STABLE_IMG_REPOSITORY}/{STABLE_IMG_NAME}",
    tag = "latest",
)

container_push(
    name = "push-cip-tagged",
    image = ":cip-with-gcloud",
    format = "Docker",
    registry = "{STABLE_IMG_REGISTRY}",
    repository = "{STABLE_IMG_REPOSITORY}/{STABLE_IMG_NAME}",
    tag = "{STABLE_IMG_TAG}",
)
